apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
apply from: '../buildsystem/extension.gradle'

android {

    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration.getAt("androidCompileSdkVersion")
    buildToolsVersion globalConfiguration.getAt("androidBuildToolsVersion")

    defaultConfig {
        minSdkVersion globalConfiguration.getAt("androidMinSdkVersion")
        targetSdkVersion globalConfiguration.getAt("androidTargetSdkVersion")

        versionCode globalConfiguration.getAt("androidVersionCode")
        versionName globalConfiguration.getAt("androidVersionName")
        archivesBaseName = "BAAD-$versionName"
        signingConfig signingConfigs.debug

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
    }

    lintOptions {
        quiet true
        abortOnError false
        ignoreWarnings true
        disable 'InvalidPackage'            //Some libraries have issues with this.
        disable 'OldTargetApi'
        //Lint gives this warning but SDK 20 would be Android L Beta.
        disable 'IconDensities'             //For testing purpose. This is safe to remove.
        disable 'IconMissingDensityFolder'  //For testing purpose. This is safe to remove.
    }

    signingConfigs {
        debug {
            storeFile file('../buildsystem/debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix ".dev"
        }
        release {
            debuggable false
        }
    }
}

dependencies {
    def firebaseVersion = "11.8.0"
    def playServicesVersion = "11.8.0"
    def androidSupportVersion = "26.1.0"
    def leakCanaryVersion = '1.5.1'

    annotationProcessor "com.google.dagger:dagger-compiler:2.0"

    implementation project(':domain')
    implementation project(':data')

    implementation "com.jakewharton:butterknife:6.0.0"
    implementation "com.squareup.picasso:picasso:2.5.2"

    implementation "com.android.support:appcompat-v7:${androidSupportVersion}"
    implementation "com.android.support:recyclerview-v7:${androidSupportVersion}"
    implementation "com.android.support:design:${androidSupportVersion}"
    implementation "com.google.android.gms:play-services-analytics:${playServicesVersion}"
    implementation "com.google.android.gms:play-services-maps:${playServicesVersion}"
    implementation "com.google.firebase:firebase-messaging:${firebaseVersion}"
    implementation "com.google.firebase:firebase-core:${firebaseVersion}"
    implementation 'com.github.daniel-stoneuk:material-about-library:2.2.3-support26.1.0'
    implementation "com.mikepenz:iconics-core:2.9.5@aar"
    implementation 'com.mikepenz:community-material-typeface:2.0.46.1@aar'
    implementation "com.google.dagger:dagger:${rootProject.ext.daggerVersion}"
    implementation "io.reactivex:rxjava:${rootProject.ext.rxJavaVersion}"
    implementation "io.reactivex:rxandroid:${rootProject.ext.rxAndroidVersion}"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    provided "org.glassfish:javax.annotation:${rootProject.ext.javaxAnnotationVersion}"

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'appcompat'
        exclude module: 'recyclerview-v7'
    })
}

apply plugin: 'com.google.gms.google-services'