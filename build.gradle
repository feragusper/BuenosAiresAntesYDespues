apply from: 'buildsystem/ci.gradle'
apply from: 'buildsystem/dependencies.gradle'

def getVersionName = { ->
    def os = System.getProperty("os.name").toLowerCase()
    def revision = new ByteArrayOutputStream()
    if (!os.contains("windows")) {
        exec {
            executable "/bin/sh"
            //noinspection GroovyAssignabilityCheck
            args "-c", "echo -n `git rev-list HEAD | wc -l | sed 's/^[ ^t]*//'`"
            standardOutput = revision;
        }
    } else {
        exec {
            executable "cmd"
            //noinspection GroovyAssignabilityCheck
            args "/c", "git rev-list HEAD | find /c /v \"\""
            standardOutput = revision;
        }
        revision = revision as String
        revision = revision.trim()
    }

    return revision
}

buildscript {
    repositories {
        jcenter()
        maven { url 'https://www.testfairy.com/maven' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'
        classpath 'com.testfairy.plugins.gradle:testfairy:1.+'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
    }
}

allprojects {
    repositories {
        jcenter()
    }
    ext {
        androidApplicationId = 'com.feragusper.buenosairesantesydespues'
        androidVersionCode = 5
        androidVersionName = '1.0.0.' + getVersionName()
        testInstrumentationRunner = "android.support.test.runner.AndroidJUnitRunner"
        testApplicationId = 'com.feragusper.buenosairesantesydespues.presentation.test'
    }
}

task wrapper(type: Wrapper) {
    description 'Creates the gradle wrapper.'
    gradleVersion '2.4'
}